plugins {
    `application`
    id("org.openjfx.javafxplugin") version "0.0.10"
    id("org.beryx.jlink") version "2.24.1"
}

dependencies {
    compile(project(":greeter-impl"))
}

javafx {
    modules = listOf("javafx.controls")
}

application {
    mainClassName = "org.example.gui/org.example.gui.HelloFX"
}

jlink {
    imageZip.set(File("$buildDir/image-zip/hellofx.zip"))
    addOptions("--bind-services", "--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages")
    if("true".equals(System.getenv("CI"))) {
        listOf("win", "linux", "mac").forEach { name ->
            targetPlatform(name) {
                jdkHome = System.getenv("JDK_HOME_" + name.toUpperCase())
                addExtraModulePath(System.getenv("OPENJFX_MODS_" + name.toUpperCase()))
            }
        }
    }
    launcher {
        name = "helloFX"
    }
}
