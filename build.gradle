// Copyright (c) XJ Music Inc. (https://xjmusic.com) All Rights Reserved.

plugins {
  id 'java'
  id 'application'
  id 'distribution'
  //
  id 'de.undercouch.download' version "4.0.4"
  id 'org.ajoberstar.grgit' version '4.1.0'
  //
  id "org.beryx.jlink" version "2.24.1" apply false
  id 'com.google.cloud.tools.jib' version '3.3.2' apply false
  id 'io.spring.dependency-management' version '1.0.15.RELEASE' apply false
  id 'org.openjfx.javafxplugin' version '0.0.14' apply false
  id 'org.panteleyev.jpackageplugin' version '1.5.2' apply false
  id 'org.springframework.boot' version '3.1.2' apply false // must match springBootVersion below
}

ext {
  jacksonAnnotationsVersion = '2.15.2'
  javaFxVersion = '17.0.8'
  jooqVersion = '3.18.5'
  junitVersion = '5.9.2'
  springBootVersion = '3.1.2'
  springTestVersion = '6.0.11'
  xjModelsVersion = '0.5.24'
  jupiterVersion = '5.8.1'
}

Properties localProperties = new Properties()
def localPropertiesFile = project.rootProject.file('gradle-local.properties')
if (localPropertiesFile.exists()) {
  localProperties.load(localPropertiesFile.newDataInputStream())
}

allprojects {
  group = 'io.xj'

  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17

  tasks.withType(JavaCompile).tap {
    configureEach {
      options.encoding = 'UTF-8'
    }
  }
}

subprojects {
  apply plugin: 'java'

  repositories {
    mavenCentral()
    mavenLocal()
    maven {
      name = 'ajoberstar-backup'
      url = 'https://ajoberstar.org/bintray-backup/'
    }
    maven {
      url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
      url = 'https://packages.confluent.io/maven/'
    }
    maven {
      url = 'https://oss.sonatype.org/content/repositories/releases'
    }
    maven {
      name = "TarsosDSP repository"
      url = "https://mvn.0110.be/releases"
    }
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/xjmusic/hub")
      credentials {
        username = localProperties.getProperty('repo.username') ?: System.getenv("GITHUB_ACTOR")
        password = localProperties.getProperty('repo.token') ?: System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

if (!project.hasProperty("jibFromImage")) {
  ext.jibFromImage = "localhost/base:latest"
}

if (!project.hasProperty("jibToImageRegistry")) {
  ext.jibToImageRegistry = "localhost"
}

test {
  useJUnitPlatform()
}
